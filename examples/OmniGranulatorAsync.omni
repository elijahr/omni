use Grain

params:
    density        {0.5, 0, 1}
    position       {0, 0, 1}
    position_range {0, 0, 1}
    length         {0.1, 0, 1}
    length_range   {0, 0, 1}
    pitch          {0, -12, 12}
    pitch_range    {0, 0, 1}

buffers:
    audio_buffer
    window_buffer

init:
    new_grain_phase     = 1
    new_grain_increment = 0
    inverse_samplerate  = 1 / samplerate
    max_num_grains      = 30
    grains              = Grains(max_num_grains) 

perform:
    stereo bool          = audio_buffer.chans == 2
    length_audio_buffer  = audio_buffer.length
    length_window_buffer = window_buffer.length

    density_scale          = density * max_num_grains
    position_samples       = position * length_audio_buffer
    position_range_samples = (position_range * 0.3333) * length_audio_buffer
    length_samples         = length * samplerate
    length_range_samples   = (length_range * 0.5) * samplerate
    pitch_range_semitone   = pitch_range * 12
    
    sample:
        if new_grain_phase >= 1:
            grains.trigger_new_grain(
                length_audio_buffer, position_samples, position_range_samples,
                length_samples, length_range_samples, pitch, pitch_range_semitone
            )
            new_grain_phase  = 0
            
            new_grain_increment = rand() * inverse_samplerate * 1.5

        out_values = grains.get_active_grains(audio_buffer, window_buffer, stereo)
        out1 = out_values[0]
        out2 = out_values[1]

        new_grain_phase += (inverse_samplerate + new_grain_increment) * density_scale
